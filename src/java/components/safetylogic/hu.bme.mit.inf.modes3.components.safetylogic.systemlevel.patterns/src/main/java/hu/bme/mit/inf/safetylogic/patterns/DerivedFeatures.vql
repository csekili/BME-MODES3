package hu.bme.mit.inf.safetylogic.patterns

import "http://hu.bme.mit.inf.safetylogic.railroadmodel"  

//XXX @QueryBasedFeature(feature = "currentlyConnected")
pattern currentlyConnected(This : RailRoadElement, connectedTo : RailRoadElement) = {
	Segment(This);
	Segment.connectedTo(This, connectedTo);
} or { 
	Turnout(This);
	Turnout.top(This, connectedTo);
} or {
	Turnout(This);
	Turnout.currentlyDivergent(This, true);
	Turnout.divergent(This, connectedTo);	
} or {
	Turnout(This);
	Turnout.currentlyDivergent(This, false);
	Turnout.straight(This, connectedTo);
}  

//pattern currentlyConnectedSegments(This : Segment, Other : Segment)

//XXX @QueryBasedFeature(feature = "connected")//, source = This, target = connectedTo, kind = "many") 
pattern connected(This : RailRoadElement, connectedTo : RailRoadElement) = {
	Segment(This);
	Segment.connectedTo(This, connectedTo);
} or {
	Turnout(This);
	Turnout.top(This, connectedTo);
} or {
	Turnout(This);
	Turnout.divergent(This, connectedTo);
} or {
	Turnout(This);
	Turnout.straight(This, connectedTo);
}

//XXX @QueryBasedFeature(feature = "viablePaths")
pattern viablePaths(This : RailRoadElement, path : Path) = {
	Segment(This);
	Segment.connectedTo(This, connected1);
	Segment.connectedTo(This, connected2);
	connected1 != connected2;
	
	Path.via(path, This);
	Path.from(path, connected1);
	Path.to(path, connected2);
} or {
	Turnout(This);
	Turnout.currentlyDivergent(This, true);
	
	Turnout.top(This, top);
	Turnout.divergent(This, divergent);
	Path.via(path, This);
	Path.to(path, divergent);
	Path.from(path, top);
} or {
	Turnout(This);
	Turnout.currentlyDivergent(This, true);
	
	Turnout.top(This, top);
	Turnout.divergent(This, divergent);
	
	Path.via(path, This);
	Path.to(path, top);
	Path.from(path, divergent);
} or {
	Turnout(This);
	Turnout.currentlyDivergent(This, false);

	Turnout.top(This, top);
	Turnout.straight(This, straight);
	Path.via(path, This);
	Path.to(path, straight);
	Path.from(path, top);

} or {
	Turnout(This);
	Turnout.currentlyDivergent(This, false);

	Turnout.top(This, top);
	Turnout.straight(This, straight);
	Path.via(path, This);
	Path.to(path, top);
	Path.from(path, straight);
}